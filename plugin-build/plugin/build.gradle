plugins {
    id "org.jetbrains.kotlin.jvm"
    id "com.gradle.plugin-publish" version "1.0.0"
    id "java-gradle-plugin"
    id "maven-publish"
    id "org.jetbrains.dokka"
}

group = "com.mikepenz.aboutlibraries.plugin"
version = rootProject.properties["version"]

gradlePlugin {
    plugins {
        aboutlibsPlugin {
            displayName = 'AboutLibraries Library Gradle Plugin'
            id = "${project.group}"
            implementationClass = "${project.group}.AboutLibrariesPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/mikepenz/AboutLibraries'
    vcsUrl = 'https://github.com/mikepenz/AboutLibraries'
    description = 'Resolve all dependencies used in a gradle module, with associated license and further information.'
    tags = ['libraries', 'licenses', 'android']
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation(libs.kotlin.stdlib)

    // parser the pom.xml files
    implementation(libs.ivy.core)

    // add better android support
    compileOnly(libs.gradle.build)
}

// generate zip file for android maven release tool
task packageFiles(type: Zip, dependsOn: 'uploadArchives') {
    def groupDir = rootProject.group.replaceAll('\\.', '/')
    from("../repo/$groupDir/$rootProject.name/$rootProject.version/")
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaHtml) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dokkaHtml.configure {
    dokkaSourceSets {
    }
}